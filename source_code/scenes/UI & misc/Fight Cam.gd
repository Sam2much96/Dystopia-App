# *************************************************
# godot3-Dystopia-game by INhumanity_arts
# Released under MIT License
# *************************************************
# Fight Cam
# Targets Multiple Enemies and Focuses on THem
# Features:
# (1) Multi enemy/player focus
# (2) Camera Shake FX

extends Camera2D

class_name FightCam


# Multi - Target Camera
export var move_speed = 0.5 # camera position lerp speed
export var zoom_speed = 0.25 # camera zoom lerp speed
export var min_zoom = 1.5 # camera wont zoom closer than this
export var max_zoom = 5 # camera wont zoom further than this
export var margin = Vector2(400, 200)  # include some buffer area around targets


var targets = []

onready var screen_size = get_viewport_rect().size

# Camer Shake FX

export var decay = 0.8  # How quickly the shaking stops [0, 1].
export var max_offset = Vector2(100, 75)  # Maximum hor/ver shake in pixels.
export var max_roll = 0.1  # Maximum rotation in radians (use sparingly).
export (NodePath) var target  # Assign the node this camera will follow.

export(float)var trauma = 0.0  # Current shake strength.
var trauma_power = 2  # Trauma exponent. Use [2, 3].

onready var noise = OpenSimplexNoise.new() # Use procedural generation class code
var noise_y = 0

# safe pointer to global singletons
onready var globals_singleton_ = get_node("/root/Globals") # : GlobalsVar

func _ready():
	
	
	# Make Global
	# using a setter getter functions
	globals_singleton_.player_cam = self
	
	# seed random number generator
	#Utils._randomize(self)
	randomize()
	noise.seed = randi()
	noise.period = 4
	noise.octaves = 2

func _process(delta):
	if !targets: 
		return
		
	# keep the camera centered between the targets
	var p = Vector2.ZERO
	for _target in targets:
		p += _target.position
	p /= targets.size()
	position = lerp(position, p, move_speed)
	# Find the zoom that will contain all targets
	var r = Rect2(position, Vector2.ONE)
	for _target in targets:
		r = r.expand(_target.position)
	r = r.grow_individual(margin.x, margin.y, margin.x, margin.y)
	#var d = max(r.size.x, r.size.y)
	var z
	if r.size.x > r.size.y * screen_size.aspect():
		z = clamp(r.size.x / screen_size.x, min_zoom, max_zoom)
	else:
		z = clamp(r.size.y / screen_size.y, min_zoom, max_zoom)
	zoom = lerp(zoom, Vector2.ONE * z, zoom_speed)
	
	
	# SHaky Cam
	if target:
		global_position = get_node(target).global_position
	if trauma:
		trauma = max(trauma - decay * delta, 0)
		shake()



# Multi-Target Cam system
func add_target(t):
	if not t in targets:
		targets.append(t)

func remove_target(t):
	if t in targets:
		targets.erase(t)

# SHaky Cam FX

func add_trauma(amount):
	trauma = min(trauma + amount, 1.0)


func shake():
	var amount = pow(trauma, trauma_power)
	rotation = max_roll * amount * rand_range(-1, 1)
	offset.x = max_offset.x * amount * rand_range(-1, 1)
	offset.y = max_offset.y * amount * rand_range(-1, 1)
	noise_y += 1
	rotation = max_roll * amount * noise.get_noise_2d(noise.seed, noise_y)
	offset.x = max_offset.x * amount * noise.get_noise_2d(noise.seed*2, noise_y)
	offset.y = max_offset.y * amount * noise.get_noise_2d(noise.seed*3, noise_y)
